buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
    maven { url "https://plugins.gradle.org/m2/" }
  }

  dependencies {
    classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version"
    classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
  }
}

repositories {
  maven { url "https://repo.spring.io/snapshot" }
  maven { url "https://repo.spring.io/milestone" }
}

apply plugin: 'kotlin-spring'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

sourceSets {
  main {
    resources {
      srcDir "config/dev"
    }
  }
  test {
    resources {
      srcDir "config/test"
    }
  }
  integrationTest {
    kotlin {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir file('src/integrationTest/kotlin')
    }
  }
}

configurations {
  integrationTestCompile.extendsFrom testCompile
  integrationTestRuntime.extendsFrom testRuntime
}

task copyFrontendArtifacts(type: Copy) {
  dependsOn ':frontend:assemble'

  from "$rootDir/frontend/dist"
  into "${processResources.destinationDir}/static"
}

processResources.dependsOn(copyFrontendArtifacts)

task integrationTest(type: Test, dependsOn: []) {
  description = 'Runs integration tests.'
  group = 'verification'
  outputs.upToDateWhen { false }
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
}

task integrationTestWithDocker(dependsOn: [":agentsComposeBuild", ":cordaComposeBuild", ":backendsComposeBuild", ":agentsComposeUp", ":cordaComposeUp", ":backendsComposeUp", integrationTest, ":backendsComposeDown", ":cordaComposeDown", ":agentsComposeDown"]) {
  description = 'Start dockerised system and runs integration tests.'
  group = 'verification'
}

integrationTest.mustRunAfter(":backendsComposeUp")

dependencies {

  // CorDapp dependencies
  // Specify your cordapp's dependencies below, including dependent cordapps
  implementation project(':cordapp')

  // app specific deps
  implementation 'com.corundumstudio.socketio:netty-socketio:1.7.13'

  //TODO: java.lang.ClassNotFoundException: javax.json.JsonValue fix
  implementation("org.apache.activemq:artemis-core-client:2.2.0") { force = true }
  implementation("org.apache.activemq:artemis-commons:2.2.0") { force = true }
  implementation "javax.json:javax.json-api"

  //Swagger
  implementation "io.springfox:springfox-swagger2:$springfox_version"
  implementation "io.springfox:springfox-swagger-ui:$springfox_version"

  // Spring Boot
  implementation 'org.springframework.boot:spring-boot-starter'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.springframework.boot:spring-boot-test'
}
